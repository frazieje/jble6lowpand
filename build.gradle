apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

mainClassName = "com.spoohapps.jble6lowpand.ScanningDaemon"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}

repositories {
    mavenCentral()
}

distributions {
    main {
        contents {
            from { 'jni/libs/arm32' }
        }
    }
}

dependencies {
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version: '2.27'
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.27'

    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.27'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    testCompile("junit:junit:4.12")
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.2")

    testRuntime("org.junit.platform:junit-platform-launcher:1.0.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.2")
    testRuntime("org.junit.vintage:junit-vintage-engine:4.12.2")
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.spoohapps.jble6lowpand.controller.ScanningDaemonController'
        )
    }
}

task copyToLib(type: Copy) {
    into "$buildDir/output/libs"
    from configurations.runtime
}

task generateJniHeaders(type: Exec) {
    def classpath = sourceSets.main.output.classesDir
    commandLine "javah", "-d", "jni/src", "-classpath", classpath, "com.spoohapps.jble6lowpand.NativeBle6LowpanIpspService"
    dependsOn classes
}

task generateInitScript(type: Copy) {
    doFirst {
        if (!System.env.'JAVA_HOME'?.trim()) {
            throw new GradleException("JAVA_HOME is not set. Please set JAVA_HOME and try again")
        }
    }
    def template_vars = [install_path: "$projectDir",
                         java_home: System.env.'JAVA_HOME'?.trim()]
    def tag = /<%.*%>/
    from "$projectDir/init.template"
    into "$buildDir/output/init"
    rename { file -> "jble6lowpand" }
    filter { line ->
        def sb = new StringBuffer()
        def matches = (line =~ /$tag/)
        def i = 0
        while (matches.find()) {
            matches.appendReplacement(sb, template_vars.get(matches[i].substring(2, matches[i].length()-2).trim()))
        }
        matches.appendTail(sb)
        return sb.toString()
    }             
}

build.dependsOn(copyToLib)
